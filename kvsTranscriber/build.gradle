apply plugin: 'java'

def repoServerUrl = System.getProperty("nexusProxyUrl") ?: System.getenv("defaultNexusProxyURL") ?: "https://sfm-nginx-proxy.internal.salesforce.com"

if (!repoServerUrl.endsWith('/')) {
    repoServerUrl += '/'
}
// In Jenkins, it uses the System.getProperty to get nexusUser and nexusPassword
def nexusUser = System.getProperty("NEXUS_USERNAME") ?: System.getenv('NEXUS_USERNAME')
def nexusPassword = System.getProperty("NEXUS_PASSWORD") ?: System.getenv('NEXUS_PASSWORD')
logger.info("nexusUser from System property: ${nexusUser}")

repositories {
        if (!System.getProperty("NEXUS_USERNAME")) {
            mavenCentral()
        }
        
        maven {
            name 'Nexus Public'
            credentials {
                username nexusUser
                password nexusPassword
            }
            url repoServerUrl.concat("nexus/content/groups/public")
        }

        maven {
            name 'Nexus - Internal'
            credentials {
                username nexusUser
                password nexusPassword
            }
            url repoServerUrl.concat("nexus/content/repositories/releases")
        }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

test {
    useJUnitPlatform()
    forkEvery = 1
}

dependencies {
    implementation fileTree(dir: 'jars', include: '*.jar')
    implementation group: 'io.reactivex.rxjava2', name: 'rxjava', version: '2.2.2'
    implementation group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.64'
    implementation group: 'io.jsonwebtoken', name: 'jjwt', version: '0.9.1'
    implementation group: 'com.amazonaws', name: 'aws-java-sdk-ssm', version: '1.11.728'
    implementation group: 'software.amazon.awssdk', name: 'transcribestreaming', version: '2.17.4'
    implementation group: 'com.amazonaws', name: 'aws-java-sdk-kinesisvideo', version: '1.11.475'
    implementation group: 'com.amazonaws', name: 'aws-lambda-java-core', version: '1.2.0'
    implementation group: 'com.amazonaws', name: 'aws-lambda-java-events', version: '2.2.7'
    implementation group: 'com.amazonaws', name: 'amazon-kinesis-video-streams-parser-library', version: '1.0.8'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.6'
    implementation group: 'software.amazon.awssdk', name: 'netty-nio-client', version: '2.2.0'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.17.1'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.17.1'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.17.1'
    implementation group: 'org.json', name: 'json', version: '20180813'
    implementation group: 'software.amazon.awssdk', name: 'auth', version: '2.8.7'
    implementation group: 'software.amazon.awssdk', name: 'sdk-core', version: '2.7.11'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.0'
    implementation group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1'
    implementation group: 'software.amazon.awssdk', name: 'connect', version: '2.17.4'
    testImplementation 'com.github.stefanbirkner:system-lambda:1.1.0'
    testImplementation 'com.github.stefanbirkner:system-rules:1.19.0'
    testImplementation 'org.mockito:mockito-inline:3.4.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

task buildZip(type: Zip) {
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtimeClasspath
    }
}
